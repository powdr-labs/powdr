use std::str::FromStr;
use std::collections::BTreeSet;
use powdr_ast::parsed::{*, asm::*, types::*};
use powdr_number::{BigInt, BigUint};
use crate::{ParserContext, unescape_string};

grammar(ctx: &ParserContext);

extern {
    type Error = String;
}

match {
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    _,
}

pub PILFile: PILFile = {
    <PilStatement*> => PILFile(<>)
};

pub ASMModule: ASMModule = {
    (<ModuleStatement>)* => ASMModule { statements: <> }
};

ModuleStatement: ModuleStatement = {
    <MachineDefinition> => ModuleStatement::SymbolDefinition(<>),
    <LetStatementAtModuleLevel> => ModuleStatement::SymbolDefinition(<>),
    <EnumDeclaration> => ModuleStatement::SymbolDefinition(SymbolDefinition {
            name: <>.name.clone(),
            value: SymbolValue::TypeDeclaration(<>),
        }),
    <Import> => ModuleStatement::SymbolDefinition(<>),
    <ModuleDefinition> => ModuleStatement::SymbolDefinition(<>),
}

ModuleDefinition: SymbolDefinition = {
    "mod" <name:Identifier> ";" => SymbolDefinition { name: name.clone(), value: Module::External(name).into() }, 
    "mod" <name:Identifier> "{" <module:ASMModule> "}" => SymbolDefinition { name, value: Module::Local(module).into() }
}

Import: SymbolDefinition = {
    "use" <path:SymbolPath> <name:( "as" <Identifier> )?> ";" =>
        SymbolDefinition {
            name: name.unwrap_or(path.name().clone().try_into().unwrap()),
            value: Import {path}.into()
        }
}

pub SymbolPath: SymbolPath = {
    <abs:"::"?> <parts:( <Part> "::" )*> <end:Part>  => {
        SymbolPath::from_parts([
            abs.map(|_| vec![Part::Named(String::new())]).unwrap_or_default(),
            parts,
            vec![end],
        ].concat())
    },
}

Part: Part = {
    "super" => Part::Super,
    <name:Identifier> => Part::Named(name),
}

// Same as SymbolPath plus we allow "::<...>" at the end.
GenericSymbolPath: (SymbolPath, Option<Vec<Type<Expression>>>) = {
    // If we "inline" SymbolPath here, we get an ambiguity error.
    <abs:"::"?> <parts:( <Part> "::" )*> <end:Part> <types:("::" "<" <TypeTermList> ">")?>  => (
        SymbolPath::from_parts([
            abs.map(|_| vec![Part::Named(String::new())]).unwrap_or_default(),
            parts,
            vec![end],
        ].concat()),
        types
    ),
}

/// Same as SymbolPath except that we do not allow 'int' and 'fe' to be parsed as identifiers.
TypeSymbolPath: SymbolPath = {
    <abs:"::"?> <parts:( <TypeSymbolPathPart> "::" )*> <end:TypeSymbolPathPart>  => {
        SymbolPath::from_parts([
            abs.map(|_| vec![Part::Named(String::new())]).unwrap_or_default(),
            parts,
            vec![end],
        ].concat())
    },
}

TypeSymbolPathPart: Part = {
    "super" => Part::Super,
    <name:NonSpecialIdentifier> => Part::Named(name),
}

LetStatementAtModuleLevel: SymbolDefinition = {
    "let" <name:GenericTypedName>  "=" <value:Expression> ";" =>
        SymbolDefinition {
            name: name.0,
            value: SymbolValue::Expression(TypedExpression{ e: value, type_scheme: name.1 })
        }
}

// ---------------------------- PIL part -----------------------------

pub PilStatement = {
    Include,
    Namespace,
    LetStatement,
    ConstantDefinition,
    PolynomialDefinition,
    PublicDeclaration,
    PolynomialConstantDeclaration,
    PolynomialConstantDefinition,
    PolynomialCommitDeclaration,
    <start:@L> <decl:EnumDeclaration> => PilStatement::EnumDeclaration(ctx.source_ref(start), decl),
    PlookupIdentityStatement,
    PermutationIdentityStatement,
    ConnectIdentityStatement,
    ExpressionStatement,
};

Include: PilStatement = {
    <start:@L> "include" <file:StringLiteral> ";" => PilStatement::Include(ctx.source_ref(start), file)
};

Namespace: PilStatement = {
    <start:@L> "namespace" <name:SymbolPath> <pol_degree:("(" <Expression> ")")?> ";" => PilStatement::Namespace(ctx.source_ref(start), name, pol_degree)
}

LetStatement: PilStatement = {
    <start:@L> "let" <name:GenericTypedName> <expr:( "=" <Expression> )?> ";" =>
        PilStatement::LetStatement(ctx.source_ref(start), name.0, name.1, expr)
}

ConstantDefinition: PilStatement = {
    <start:@L> "constant" <id:ConstantIdentifier> "=" <expr:Expression> ";" => PilStatement::ConstantDefinition(ctx.source_ref(start), id, expr)
}

PolynomialDefinition: PilStatement = {
    <start:@L> PolCol <id:Identifier> "=" <expr:Expression> ";" => PilStatement::PolynomialDefinition(ctx.source_ref(start), id, expr)
}

PublicDeclaration: PilStatement = {
    <start:@L> "public" <id:Identifier> "="
        <poly:NamespacedPolynomialReference>
        <expr1:("[" <Expression> "]")?>
        "(" <expr2:Expression> ")" ";" => PilStatement::PublicDeclaration(ctx.source_ref(start), id, poly, expr1, expr2)
}

PolynomialConstantDeclaration: PilStatement = {
    <start:@L> PolCol ConstantFixed <list:PolynomialNameList> ";" => PilStatement::PolynomialConstantDeclaration(ctx.source_ref(start), list)
}

PolynomialConstantDefinition: PilStatement = {
    <start:@L> PolCol ConstantFixed <id:Identifier> <def:FunctionDefinition> ";"
        => PilStatement::PolynomialConstantDefinition(ctx.source_ref(start), id, def)
}

FunctionDefinition: FunctionDefinition = {
    <start:@L> "(" <params:ParameterList> ")" <body:BracedExpression> => FunctionDefinition::Expression(Expression::LambdaExpression(ctx.source_ref(start), LambdaExpression{kind: FunctionKind::Pure, params, body})),
    <start:@L> "=" <array:ArrayLiteralExpression> => FunctionDefinition::Array(array),
}

ParameterList: Vec<Pattern> = {
    PatternList
}

ArrayLiteralExpression: ArrayExpression = {
    <ArrayLiteralExpression> "+" <ArrayLiteralTerm> => ArrayExpression::concat(<>),
    ArrayLiteralTerm,
}

ArrayLiteralTerm: ArrayExpression = {
    "[" <ExpressionList> "]" => ArrayExpression::value(<>),
    "[" <ExpressionList> "]" "*" => ArrayExpression::repeated_value(<>),
}

PolynomialCommitDeclaration: PilStatement = {
    <start:@L> PolCol CommitWitness <stage:Stage?> <list:PolynomialNameList> ";" => PilStatement::PolynomialCommitDeclaration(ctx.source_ref(start), stage, list, None),
    <start:@L> PolCol CommitWitness <stage:Stage?> <name:PolynomialName> "(" <params:ParameterList> ")" "query" <body:BoxedExpression> ";"
     => PilStatement::PolynomialCommitDeclaration(
        ctx.source_ref(start),
        stage,
        vec![name],
        Some(FunctionDefinition::Expression(Expression::LambdaExpression(ctx.source_ref(start), LambdaExpression{kind: FunctionKind::Query, params, body})))
    )
}

Stage: u32 = {
    "stage" "(" <Number> ")" => u32::try_from(<>).unwrap()
}

PolynomialNameList: Vec<PolynomialName> = {
    <mut list:( <PolynomialName> "," )*> <end:PolynomialName>  => { list.push(end); list }
}

PolynomialName: PolynomialName = {
    <name:Identifier> <array_size:( "[" <Expression> "]" )?> => PolynomialName{<>}
}

PlookupIdentityStatement: PilStatement = {
    <PlookupIdentity> ";"
}

PlookupIdentity: PilStatement = {
    <start:@L> <se1:SelectedExpressions> "in" <se2:SelectedExpressions> => PilStatement::PlookupIdentity(ctx.source_ref(start), se1, se2)
}

SelectedExpressions: SelectedExpressions<Expression> = {
    <selector:Expression?> "{" <expressions:ExpressionList> "}" => SelectedExpressions{<>},
    Expression => SelectedExpressions{selector: None, expressions: vec![<>]},
}

PermutationIdentityStatement: PilStatement = {
    <PermutationIdentity> ";"
}

PermutationIdentity: PilStatement = {
    <start:@L> <se1:SelectedExpressions> "is" <se2:SelectedExpressions> => PilStatement::PermutationIdentity(ctx.source_ref(start), se1, se2)
}

ConnectIdentityStatement: PilStatement = {
    <ConnectIdentity> ";"
}

ConnectIdentity: PilStatement = {
    <start:@L> "{" <list1:ExpressionList> "}" "connect" "{" <list2:ExpressionList> "}" => PilStatement::ConnectIdentity(ctx.source_ref(start), list1, list2)
}

ExpressionStatement: PilStatement = {
    <ExpressionStatementWithoutSemicolon> ";"
}

ExpressionStatementWithoutSemicolon: PilStatement = {
    <start:@L> <expr:Expression> => PilStatement::Expression(ctx.source_ref(start), expr)
}

PolCol = {
    "pol", "col"
} 

CommitWitness = {
    "commit", "witness"
} 

ConstantFixed = {
    "constant", "fixed"
}

GenericTypedName: (String, Option<TypeScheme<Expression>>) = {
    <name:Identifier> => (name, None),
    <vars:("<" <TypeVarBounds> ">")?> <name:Identifier> <ty:(":" <Type>)> =>
        (name, Some(TypeScheme{ vars: vars.unwrap_or_default(), ty }))
}


// ---------------------------- ASM part -----------------------------

MachineDefinition: SymbolDefinition = {
    "machine" <name:Identifier> <params:MachineParams> <properties:("with" <MachineProperties>)?> "{" <statements:(MachineStatement)*> "}" => SymbolDefinition { name, value: Machine { params, properties: properties.unwrap_or_default(), statements}.into() },
}

MachineProperties: MachineProperties = {
    <mut list:( <MachineProperty> "," )*> <end:MachineProperty> ","? =>? {
        list.push(end);
        MachineProperties::try_from(list).map_err(|error| lalrpop_util::ParseError::User { error })
    }
}

MachineProperty: (String, Expression) = {
    <name:Identifier> ":" <value:Expression> => (name, value),
}

MachineParams: MachineParams = {
    => MachineParams::default(),
    "(" ")" => MachineParams::default(),
    "(" <mut list:( <Param> "," )*> <end:Param> ","? ")" =>? {
        list.push(end);
        MachineParams::try_from(list).map_err(|error| lalrpop_util::ParseError::User { error })
    }
}

MachineStatement: MachineStatement = {
    Submachine,
    RegisterDeclaration,
    InstructionDeclaration,
    LinkDeclaration,
    PilStatementInMachine,
    FunctionDeclaration,
    OperationDeclaration,
}

PilStatementInMachine: MachineStatement = {
    <start:@L> <stmt:PilStatement> => MachineStatement::Pil(ctx.source_ref(start), stmt)
}

Submachine: MachineStatement = {
    <start:@L> <path:SymbolPath> <id:Identifier> <args:("(" <ExpressionList> ")")?> ";" => MachineStatement::Submachine(ctx.source_ref(start), path, id, args.unwrap_or_default()),
}

pub RegisterDeclaration: MachineStatement = {
    // TODO default update
    <start:@L> "reg" <id:Identifier> <flag:( "[" <RegisterFlag> "]" )?> ";" => MachineStatement::RegisterDeclaration(ctx.source_ref(start), id, flag)

}

RegisterFlag: RegisterFlag = {
    "@pc" => RegisterFlag::IsPC,
    "<=" => RegisterFlag::IsAssignment,
    "@r" => RegisterFlag::IsReadOnly,
}

pub InstructionDeclaration: MachineStatement = {
    <start:@L> "instr" <id:Identifier> <instr:Instruction> => MachineStatement::InstructionDeclaration(ctx.source_ref(start), id, instr)
}

pub Instruction: Instruction = {
    <params:Params> <body:InstructionBody> => Instruction { params, body }
}

pub LinkDeclaration: MachineStatement = {
    <start:@L> "link" <flag:Expression> "=>" <to:CallableRef> ";" => MachineStatement::LinkDeclaration(ctx.source_ref(start), LinkDeclaration { flag, to, is_permutation: false, }),
    <start:@L> "link" <flag:Expression> "~>" <to:CallableRef> ";" => MachineStatement::LinkDeclaration(ctx.source_ref(start), LinkDeclaration { flag, to, is_permutation: true, }),
}

pub InstructionBody: InstructionBody = {
    "{}" => InstructionBody::Local(vec![]),
    "{" <InstructionBodyElements> "}" => InstructionBody::Local(<>),
    "=" <f_ref:CallableRef> ";" => InstructionBody::CallablePlookup(f_ref),
    "~" <f_ref:CallableRef> ";" => InstructionBody::CallablePermutation(f_ref),
}

pub CallableRef: CallableRef = {
    <instance:Identifier> "." <callable:Identifier> <params:CallableParams> => CallableRef { instance, callable, params },
}

CallableParams: CallableParams = {
    <inputs: ExpressionList> "->" <output: ExpressionList> => CallableParams::new(inputs, output),
    // we can ommit the arrow if there are no outputs
    <inputs: ExpressionList> => CallableParams::new(inputs, vec![])
}

InstructionBodyElements: Vec<PilStatement> = {
    <mut list:( <InstructionBodyElement> "," )*> <end:InstructionBodyElement>  => { list.push(end); list },
    => vec![]
}

InstructionBodyElement: PilStatement = {
    PlookupIdentity,
    PermutationIdentity,
    ExpressionStatementWithoutSemicolon,
}

Params: Params<Param> = {
    <_input: ParamList> "->" <output: ParamList> => Params::new(_input, output),
    // we can ommit the arrow if there are no outputs
    <_input: ParamList> => Params::new(_input, vec![])
}

ParamList: Vec<Param> = {
    => vec![],
    <mut list:( <Param> "," )*> <end:Param>  => { list.push(end); list }
}

Param: Param = {
    <name: Identifier> <index:("[" <Number> "]")?> <ty:(":" <SymbolPath>)?> => Param{<>}
}

FunctionDeclaration: MachineStatement = {
    <start:@L> "function" <id:Identifier> <params:Params> "{" <stmt:(<FunctionStatement>)*> "}" => MachineStatement::FunctionDeclaration(ctx.source_ref(start), id, params, stmt)
}

OperationDeclaration: MachineStatement = {
    <start:@L> "operation" <id:Identifier> <op:OperationId> <params:Params> ";" => MachineStatement::OperationDeclaration(ctx.source_ref(start), id, op, params)
}

OperationId: OperationId = {
    "<" <id:Number> ">" => OperationId { id: Some(id.into()) },
    => OperationId { id: None }
}

pub FunctionStatement: FunctionStatement = {
    AssignmentStatement,
    LabelStatement,
    DebugDirectiveStatement,
    ReturnStatement,
    InstructionStatement,
}

AssignmentStatement: FunctionStatement = {
    <start:@L> <ids:IdentifierList> <op:AssignOperator> <expr:BoxedExpression> ";" => FunctionStatement::Assignment(ctx.source_ref(start), ids, op, expr)
}

IdentifierList: Vec<String> = {
    <mut list:( <Identifier> "," )*> <end:Identifier>  => { list.push(end); list },
    => vec![]
}

AssignOperator: Option<Vec<AssignmentRegister>> = {
    "<==" => None,
    "<=" <AssignmentRegisterList> "=" => Some(<>)
}

AssignmentRegisterList: Vec<AssignmentRegister> = {
    <mut list:( <AssignmentRegister> "," )*> <end:AssignmentRegister>  => { list.push(end); list },
    => vec![]
}

AssignmentRegister: AssignmentRegister = {
    <Identifier> => AssignmentRegister::Register(<>),
    "_" => AssignmentRegister::Wildcard,
}

ReturnStatement: FunctionStatement = {
    <start:@L> "return" <list:ExpressionList> ";" => FunctionStatement::Return(ctx.source_ref(start), list)
}

InstructionStatement: FunctionStatement = {
    <start:@L> <id:Identifier> <list:ExpressionList> ";" => FunctionStatement::Instruction(ctx.source_ref(start), id, list)
}

DebugDirectiveStatement: FunctionStatement = {
    <start:@L> ".debug" "file" <n:UnsignedInteger> <d:StringLiteral> <f:StringLiteral> ";"
        => FunctionStatement::DebugDirective(ctx.source_ref(start), DebugDirective::File(n.try_into().unwrap(), d, f)),
    <start:@L> ".debug" "loc" <f:UnsignedInteger> <line:UnsignedInteger> <col:UnsignedInteger> ";"
        => FunctionStatement::DebugDirective(ctx.source_ref(start), DebugDirective::Loc(f.try_into().unwrap(), line.try_into().unwrap(), col.try_into().unwrap())),
    <start:@L> ".debug" "insn" <insn:StringLiteral> ";"
        => FunctionStatement::DebugDirective(ctx.source_ref(start), DebugDirective::OriginalInstruction(insn)),
}

LabelStatement: FunctionStatement = {
    <start:@L> <id:Identifier> ":" => FunctionStatement::Label(ctx.source_ref(start), id)
}

// ---------------------------- Expressions -----------------------------

ExpressionList: Vec<Expression> = {
    => vec![],
    <mut list:( <Expression> "," )*> <end:Expression>  => { list.push(end); list }
}

Expression: Expression = {
    BoxedExpression => *<>,
}

BoxedExpression: Box<Expression> = {
    LambdaExpression,
}

LambdaExpression: Box<Expression> = {
    <start:@L> <kind:FunctionKind> "||" <body:BoxedExpression> => ctx.to_expr_with_source_ref(LambdaExpression{kind, params: vec![], body}, start),
    <start:@L> <kind:FunctionKind> "|" <params:ParameterList> "|" <body:BoxedExpression> => ctx.to_expr_with_source_ref(LambdaExpression{kind, params, body}, start),
    LogicalOr
}

FunctionKind: FunctionKind = {
    => FunctionKind::Pure,
    "query" => FunctionKind::Query,
    "constr" => FunctionKind::Constr,
}

LogicalOr: Box<Expression> = {
    <start:@L> <left:LogicalOr> "||" <right:LogicalAnd> => ctx.to_expr_with_source_ref(BinaryOperation { left, op: BinaryOperator::LogicalOr, right }, start),
    LogicalAnd,
}

LogicalAnd: Box<Expression> = {
    <start:@L> <left:LogicalAnd> "&&" <right:Comparison> => ctx.to_expr_with_source_ref(BinaryOperation { left, op: BinaryOperator::LogicalAnd, right }, start),
    Comparison,
}

Comparison: Box<Expression> = {
    <start:@L> <left:BinaryOr> <op:ComparisonOp> <right:BinaryOr> => ctx.to_expr_with_source_ref(BinaryOperation { left, op: op, right }, start),
    BinaryOr
}

ComparisonOp: BinaryOperator = {
    "<" => BinaryOperator::Less,
    "<=" => BinaryOperator::LessEqual,
    "==" => BinaryOperator::Equal,
    "=" => BinaryOperator::Identity,
    "!=" => BinaryOperator::NotEqual,
    ">=" => BinaryOperator::GreaterEqual,
    ">" => BinaryOperator::Greater,
}

BinaryOr: Box<Expression> = {
    <start:@L> <left:BinaryOr> <op:BinaryOrOp> <right:BinaryXor> => ctx.to_expr_with_source_ref(BinaryOperation { left, op, right }, start),
    BinaryXor,
}

BinaryOrOp: BinaryOperator = {
    "|" => BinaryOperator::BinaryOr,
}

BinaryXor: Box<Expression> = {
    <start:@L> <left:BinaryXor> <op:BinaryXorOp> <right:BinaryAnd> => ctx.to_expr_with_source_ref(BinaryOperation { left, op, right }, start),
    BinaryAnd,
}

BinaryXorOp: BinaryOperator = {
    "^" => BinaryOperator::BinaryXor,
}

BinaryAnd: Box<Expression> = {
    <start:@L> <left:BinaryAnd> <op:BinaryAndOp> <right:BitShift> => ctx.to_expr_with_source_ref(BinaryOperation { left, op, right }, start),
    BitShift,
}

BinaryAndOp: BinaryOperator = {
    "&" => BinaryOperator::BinaryAnd,
}

BitShift: Box<Expression> = {
    <start:@L> <left:BitShift> <op:BitShiftOp> <right:Sum> => ctx.to_expr_with_source_ref(BinaryOperation { left, op, right }, start),
    Sum,
}

BitShiftOp: BinaryOperator = {
    "<<" => BinaryOperator::ShiftLeft,
    ">>" => BinaryOperator::ShiftRight,
}

Sum: Box<Expression> = {
    <start:@L> <left:Sum> <op:SumOp> <right:Product> => ctx.to_expr_with_source_ref(BinaryOperation { left, op, right }, start),
    Product,
}

SumOp: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
}

Product: Box<Expression> = {
    <start:@L> <left:Product> <op:ProductOp> <right:Power> => ctx.to_expr_with_source_ref(BinaryOperation { left, op, right }, start),
    Power,
}

ProductOp: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
    "%" => BinaryOperator::Mod,
}

Power: Box<Expression> = {
    <start:@L> <left:Term> <op:PowOp> <right:Power> => ctx.to_expr_with_source_ref(BinaryOperation { left, op, right }, start),
    Unary,
}

PowOp: BinaryOperator = {
    "**" => BinaryOperator::Pow,
}

Unary: Box<Expression> = {
    <start:@L> <op:PrefixUnaryOp> <expr:PostfixUnary> => ctx.to_expr_with_source_ref(UnaryOperation{op, expr}, start),
    PostfixUnary,
}

PrefixUnaryOp: UnaryOperator = {
    "-" => UnaryOperator::Minus,
    "!" => UnaryOperator::LogicalNot,
}

PostfixUnary: Box<Expression> = {
    <start:@L> <t:Term> <op:PostfixUnaryOp> => ctx.to_expr_with_source_ref(UnaryOperation{op, expr: t}, start),
    Term,
}

PostfixUnaryOp: UnaryOperator = {
    "'" => UnaryOperator::Next,
}

Term: Box<Expression> = {
    <start:@L> <i:IndexAccess> => ctx.to_expr_with_source_ref(i, start),
    <start:@L> <f:FunctionCall> => ctx.to_expr_with_source_ref(f, start),
    <start:@L> <i:ConstantIdentifier> => ctx.to_expr_with_source_ref(NamespacedPolynomialReference::from_identifier(i), start),
    <start:@L> <g:GenericReference> => ctx.to_expr_with_source_ref(g, start),
    <start:@L> <p:PublicIdentifier> => Box::new(Expression::PublicReference(ctx.source_ref(start), p)),
    <start:@L> <value:Number> => ctx.to_expr_with_source_ref(Number {value: value.into(), type_: None}, start),
    <start:@L> <s:StringLiteral> => Box::new(Expression::String(ctx.source_ref(start), s)),
    MatchExpression,
    IfExpression,
    BlockExpression,
    <start:@L> "[" <items:ExpressionList> "]" => ctx.to_expr_with_source_ref(ArrayLiteral{items}, start),
    <start:@L> "(" <head:Expression> "," <tail:ExpressionList> ")" => { let mut list = vec![head]; list.extend(tail); Box::new(Expression::Tuple(ctx.source_ref(start), list)) },
    <start:@L> "(" ")" => Box::new(Expression::Tuple(ctx.source_ref(start), vec![])),
    "(" <BoxedExpression> ")",
    <start:@L> "${" <b:BoxedExpression> "}" => Box::new(Expression::FreeInput(ctx.source_ref(start), b))
}

IndexAccess: IndexAccess = {
    <array:Term> "[" <index:BoxedExpression> "]" => IndexAccess{<>},
}

FunctionCall: FunctionCall = {
    <function:Term> "(" <arguments:ExpressionList> ")" => FunctionCall {<>},
}

NamespacedPolynomialReference: NamespacedPolynomialReference = {
    <SymbolPath> => <>.into(),
    <namespace:Identifier> "." <name:Identifier> => SymbolPath::from_parts([namespace, name].into_iter().map(Part::Named)).into(),
}

GenericReference: NamespacedPolynomialReference = {
    <namespace:Identifier> "." <name:Identifier> => SymbolPath::from_parts([namespace, name].into_iter().map(Part::Named)).into(),
    <path:GenericSymbolPath> => NamespacedPolynomialReference{path: path.0, type_args: path.1},
}

MatchExpression: Box<Expression> = {
    <start:@L> "match" <scrutinee:BoxedExpression> "{" <arms:MatchArms> "}" => ctx.to_expr_with_source_ref(MatchExpression{scrutinee, arms}, start),
}

MatchArms: Vec<MatchArm> = {
    => vec![],
    <mut list:( <MatchArm> "," )*> <end:MatchArm> ","?  => { list.push(end); list }
}

MatchArm: MatchArm = {
    <pattern:Pattern> "=>" <value: Expression> => MatchArm{pattern, value},
}

IfExpression: Box<Expression> = {
    <start:@L> "if" <condition:BoxedExpression>
        <body:BracedExpression>
        "else"
        <else_body:BracedExpression> => ctx.to_expr_with_source_ref(IfExpression{condition, body, else_body}, start),
}

BlockExpression: Box<Expression> = {
    <start:@L> "{" <statements:StatementInsideBlock+> <expr:BoxedExpression> "}" => ctx.to_expr_with_source_ref(BlockExpression{statements, expr}, start),
}

BracedExpression: Box<Expression> = {
    <start:@L> "{" <statements:StatementInsideBlock+> <expr:BoxedExpression> "}" => ctx.to_expr_with_source_ref(BlockExpression{statements, expr}, start),
    <start:@L> "{" <expr:BoxedExpression> "}" => ctx.to_expr_with_source_ref(BlockExpression{statements: vec![], expr}, start),
}

StatementInsideBlock: StatementInsideBlock = {
    "let" <pattern:Pattern> <value:("=" <Expression>)?> ";" => StatementInsideBlock::LetStatement(LetStatementInsideBlock{<>}),
    <Expression> ";" => StatementInsideBlock::Expression(<>),
}

// ---------------------------- Pattern -----------------------------

Pattern: Pattern = {
    "_" => Pattern::CatchAll,
    Number => Pattern::Number(<>.into()),
    "-" <Number> => Pattern::Number(-BigInt::from(<>)),
    StringLiteral => Pattern::String(<>),
    TuplePattern,
    ArrayPattern,
    // We never parse Pattern::Variable directly. Instead, we
    // parse all identifiers (and symbol paths) as Pattern::Enum.
    // At symbol resolution stage, single identifiers that do not
    // exist in the current scope are then turned into Pattern::Variable
    // to allow `match x { (y, None) => ... }` to bind the first component
    // to `y` and match the enum variant `None` at the same time.
    EnumPattern,
}

PatternIncludingEllipsis: Pattern = {
    Pattern => <>,
    ".." => Pattern::Ellipsis,
}

TuplePattern: Pattern = {
    "(" ")" => Pattern::Tuple(vec![]),
    "(" <mut items:(<Pattern> ",")+> <last:Pattern> ")" => Pattern::Tuple({items.push(last); items})
}

ArrayPattern: Pattern = {
    "[" "]" => Pattern::Array(vec![]),
    "[" <mut items:(<PatternIncludingEllipsis> ",")*> <last:PatternIncludingEllipsis> "]" => Pattern::Array({items.push(last); items})
}

EnumPattern: Pattern = {
    <SymbolPath> <( "(" <PatternList> ")" )?> => Pattern::Enum(<>)
}

PatternList: Vec<Pattern> = {
    <mut list:( <Pattern> "," )*> <end:Pattern>  => { list.push(end); list },
    => vec![]
}

// ---------------------------- Type Declarations -----------------------------

EnumDeclaration: EnumDeclaration<Expression> = {
    "enum" <name:Identifier> <type_vars:("<" <TypeVarBounds> ">")?> "{" <variants:EnumVariants> "}"
        => EnumDeclaration{name, type_vars: type_vars.unwrap_or_default(), variants}
}

EnumVariants: Vec<EnumVariant<Expression>> = {
    => vec![],
    <mut list:( <EnumVariant> "," )*> <end:EnumVariant> ","?  => { list.push(end); list }
}

EnumVariant: EnumVariant<Expression> = {
    <name:Identifier> <fields:("(" <TypeTermList> ")")?> => EnumVariant{<>}
}

// ---------------------------- Type Names -----------------------------

pub Type: Type<Expression> = {
    <params:TypeTermList> "->" <value:TypeTermBox> => Type::Function(FunctionType{<>}),
    TypeTerm
}

TypeTermList: Vec<Type<Expression>> = {
    => vec![],
    <mut list:( <TypeTerm> "," )*> <end:TypeTerm>  => { list.push(end); list }
}

TypeTermBox: Box<Type<Expression>> = {
    TypeTerm => Box::new(<>)
}

TypeTerm: Type<Expression> = {
    // The parser parses all identifiers as NamedTypes, some are translated
    // to TypeVars later.
    TypeSymbolPath ("<" <TypeTermList> ">")? => Type::NamedType(<>),
    "!" => Type::Bottom,
    "bool" => Type::Bool,
    "int" => Type::Int,
    "fe" => Type::Fe,
    "string" => Type::String,
    "col" => Type::Col,
    "expr" => Type::Expr,
    <base:TypeTerm> "[" <length:Expression?> "]" => Type::Array(ArrayType{base: Box::new(base), length}),
    "(" <mut items:( <TypeTerm> "," )+> <end:TypeTerm> ")" => { items.push(end); Type::Tuple(TupleType{items}) },
    "(" ")" => Type::Tuple(TupleType{items: vec![]}),
    "(" <Type> ")",
}

TypeVar: String = {
    UppercaseIdentifier => <>,
}


// bounds should be SymbolPaths in the future.
pub TypeVarBounds: TypeBounds = {
    => Default::default(),
    <list:( <TypeVarWithBounds> "," )*> <end:TypeVarWithBounds> => TypeBounds::new(list.into_iter().chain(std::iter::once(end)))
}

TypeVarWithBounds: (String, BTreeSet<String>) = {
    <TypeVar> <TypeBoundsList> => (<>)
}

TypeBoundsList: BTreeSet<String> = {
    => Default::default(),
    ":" <list:( <Identifier> "+" )*> <end:Identifier>  => list.into_iter().chain(std::iter::once(end)).collect(),
}


// ---------------------------- Terminals -----------------------------



StringLiteral: String = {
    r#""[^\\"\n\r]*(\\[tnfbrx'"\\0-9][^\\"\n\r]*)*""# => unescape_string(<>)
}

Identifier: String = {
    r"[a-z_][a-zA-Z$_0-9@]*" => <>.to_string(),
    UppercaseIdentifier,
    SpecialIdentifier => <>.to_string(),
}

NonSpecialIdentifier: String = {
    r"[a-z_][a-zA-Z$_0-9@]*" => <>.to_string(),
    UppercaseIdentifier => <>.to_string(),
}

UppercaseIdentifier: String = {
    r"[A-Z][a-zA-Z$_0-9@]*" => <>.to_string(),
}

// These identifier are special in the way that the lexer would treat them as keywords,
// but these keywords can never occur in a place where an identifier can be expected,
// so we allow them as identifiers as well.
SpecialIdentifier: &'input str = {
    "file",
    "loc",
    "insn",
    "int",
    "fe",
    "expr",
    "bool",
}

ConstantIdentifier: String = {
    r"%[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.to_string(),
}

PublicIdentifier: String = {
    r":[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.strip_prefix(":").unwrap().to_string()
}

Number: BigUint = {
    r"[0-9][0-9_]*" => BigUint::from_str(&<>.replace('_', "")).unwrap().into(),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => BigUint::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap().into(),
}

UnsignedInteger: BigUint = {
    r"[0-9][0-9_]*" => BigUint::from_str(&<>.replace('_', "")).unwrap(),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => BigUint::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap(),
}
