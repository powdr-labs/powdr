use std::str::FromStr;
use ast::parsed::{*, asm::*};
use number::{AbstractNumberType, FieldElement};
use num_traits::Num;

grammar<T> where T: FieldElement;

match {
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    _,
}

pub PILFile: PILFile<T> = {
    (<PilStatement> ";")* => PILFile(<>)

};

pub ASMModule: ASMModule<T> = {
    (<ModuleStatement>)* => ASMModule { statements: <> }
};

ModuleStatement: ModuleStatement<T> = {
    <MachineDefinition> => ModuleStatement::SymbolDefinition(<>),
    <Import> => ModuleStatement::SymbolDefinition(<>),
    <ModuleDefinition> => ModuleStatement::SymbolDefinition(<>),
}

ModuleDefinition: SymbolDefinition<T> = {
    "mod" <name:Identifier> ";" => SymbolDefinition { name: name.clone(), value: Module::External(name).into() }, 
    "mod" <name:Identifier> "{" <module:ASMModule> "}" => SymbolDefinition { name, value: Module::Local(module).into() }
}

Import: SymbolDefinition<T> = {
    "use" <path:SymbolPath> <name:( "as" <Identifier> )?> ";" => SymbolDefinition { name: name.unwrap_or(path.parts.back().unwrap().clone().try_into().unwrap()), value: Import {path}.into() }
}

pub SymbolPath: SymbolPath = {
    <mut parts:( <Part> "::" )*> <end:Part>  => { parts.push(end); SymbolPath { parts: parts.into() } },
}

Part: Part = {
    "super" => Part::Super,
    <name:Identifier> => Part::Named(name),
}

// ---------------------------- PIL part -----------------------------

pub PilStatement = {
    Include,
    Namespace,
    LetStatement,
    ConstantDefinition,
    PolynomialDefinition,
    PublicDeclaration,
    PolynomialConstantDeclaration,
    PolynomialConstantDefinition,
    PolynomialCommitDeclaration,
    PolynomialIdentity,
    PlookupIdentity,
    PermutationIdentity,
    ConnectIdentity,
    ExpressionStatement,
};

Include: PilStatement<T> = {
    <start:@L> "include" <file:StringLiteral> => PilStatement::Include(<>)
};

Namespace: PilStatement<T> = {
    <start:@L> "namespace" <name:Identifier> "(" <pol_degree:Expression> ")" => PilStatement::Namespace(<>)
}

LetStatement: PilStatement<T> = {
    <@L> "let" <Identifier> <( "=" <Expression> )?> => PilStatement::LetStatement(<>)
}

ConstantDefinition: PilStatement<T> = {
    <@L> "constant" <ConstantIdentifier> "=" <Expression> => PilStatement::ConstantDefinition(<>)
}

PolynomialDefinition: PilStatement<T> = {
    <@L> PolCol <Identifier> "=" <Expression> => PilStatement::PolynomialDefinition(<>)
}


PublicDeclaration: PilStatement<T> = {
    <@L> "public" <Identifier> "="
        <NamespacedPolynomialReference>
        <("[" <Expression> "]")?>
        "(" <Expression> ")" => PilStatement::PublicDeclaration(<>)
}

PolynomialConstantDeclaration: PilStatement<T> = {
    <@L> PolCol ConstantFixed <PolynomialNameList> => PilStatement::PolynomialConstantDeclaration(<>)
}

PolynomialConstantDefinition: PilStatement<T> = {
    <@L> PolCol ConstantFixed <Identifier> <FunctionDefinition>
        => PilStatement::PolynomialConstantDefinition(<>)
}

FunctionDefinition: FunctionDefinition<T> = {
    "(" <params:ParameterList> ")" "{" <body:BoxedExpression> "}" => FunctionDefinition::Expression(Expression::LambdaExpression(LambdaExpression{params, body})),
    "=" <ArrayLiteralExpression> => FunctionDefinition::Array(<>),
}

ParameterList: Vec<String> = {
    <mut list:( <Identifier> "," )*> <end:Identifier>  => { list.push(end); list },
    => vec![]
}

ArrayLiteralExpression: ArrayExpression<T> = {
    <ArrayLiteralExpression> "+" <ArrayLiteralTerm> => ArrayExpression::concat(<>),
    ArrayLiteralTerm,
}

ArrayLiteralTerm: ArrayExpression<T> = {
    "[" <ExpressionList> "]" => ArrayExpression::value(<>),
    "[" <ExpressionList> "]" "*" => ArrayExpression::repeated_value(<>),
}

PolynomialCommitDeclaration: PilStatement<T> = {
    <@L> PolCol CommitWitness <PolynomialNameList> => PilStatement::PolynomialCommitDeclaration(<>, None, false),
    <@L> PolCol "public" <PolynomialNameList> => PilStatement::PolynomialCommitDeclaration(<>, None, true),
    <start:@L> PolCol CommitWitness <name:PolynomialName> "(" <param:ParameterList> ")" "query" <value:Expression>
     => PilStatement::PolynomialCommitDeclaration(start, vec![name], Some(FunctionDefinition::Query(param, value)), false)
}

PolynomialIdentity: PilStatement<T> = {
    <start:@L> <attr:Attribute?> <l:BoxedExpression> "=" <r:BoxedExpression> => PilStatement::PolynomialIdentity(start, attr, Expression::BinaryOperation(l, BinaryOperator::Sub, r))
}

PolynomialNameList: Vec<PolynomialName<T>> = {
    <mut list:( <PolynomialName> "," )*> <end:PolynomialName>  => { list.push(end); list }
}

PolynomialName: PolynomialName<T> = {
    <name:Identifier> <array_size:( "[" <Expression> "]" )?> => PolynomialName{<>}
}

PlookupIdentity: PilStatement<T> = {
    <@L> <Attribute?> <SelectedExpressions> "in" <SelectedExpressions> => PilStatement::PlookupIdentity(<>)
}

SelectedExpressions: SelectedExpressions<Expression<T>> = {
    <selector:Expression?> "{" <expressions:ExpressionList> "}" => SelectedExpressions{<>},
    Expression => SelectedExpressions{selector: None, expressions: vec![<>]},
}

Attribute: String = {
    "#[" <Identifier> "]" => <>.to_string()
}

PermutationIdentity: PilStatement<T> = {
    <@L> <Attribute?> <SelectedExpressions> "is" <SelectedExpressions> => PilStatement::PermutationIdentity(<>)
}

ConnectIdentity: PilStatement<T> = {
    <@L> "{" <ExpressionList> "}" "connect" "{" <ExpressionList> "}" => PilStatement::ConnectIdentity(<>)
}

ExpressionStatement: PilStatement<T> = {
    <@L> <Expression> => PilStatement::Expression(<>)
}

PolCol = {
    "pol", "col"
} 

CommitWitness = {
    "commit", "witness"
} 

ConstantFixed = {
    "constant", "fixed"
} 

// ---------------------------- ASM part -----------------------------

MachineDefinition: SymbolDefinition<T> = {
    <start:@L> "machine" <name:Identifier> <arguments:MachineArguments> "{" <statements:(MachineStatement)*> "}" => SymbolDefinition { name, value: Machine { arguments, statements}.into() }
}

MachineArguments: MachineArguments = {
    "(" "_" "," "_" ")" => MachineArguments::default(),
    "(" <latch:Identifier> "," "_" ")" => MachineArguments { latch: Some(latch), operation_id: None },
    "(" "_" "," <operation_id:Identifier> ")" => MachineArguments { latch: None, operation_id: Some(operation_id) },
    "(" <latch:Identifier> "," <operation_id:Identifier> ")" => MachineArguments { latch: Some(latch), operation_id: Some(operation_id) },
    => MachineArguments::default(),
}

MachineStatement: MachineStatement<T> = {
    Degree,
    Submachine,
    RegisterDeclaration,
    InstructionDeclaration,
    LinkDeclaration,
    PilStatementWithSemiColon,
    FunctionDeclaration,
    OperationDeclaration,
}

PilStatementWithSemiColon: MachineStatement<T> = {
    <@L> <PilStatement> ";" => MachineStatement::Pil(<>)
}

Degree: MachineStatement<T> = {
    <@L> "degree" <Integer> ";" => MachineStatement::Degree(<>)
}

Submachine: MachineStatement<T> = {
    <@L> <SymbolPath> <Identifier> ";" => MachineStatement::Submachine(<>)
}

pub RegisterDeclaration: MachineStatement<T> = {
    // TODO default update
    <@L> "reg" <Identifier> <( "[" <RegisterFlag> "]" )?> ";" => MachineStatement::RegisterDeclaration(<>)

}

RegisterFlag: RegisterFlag = {
    "@pc" => RegisterFlag::IsPC,
    "<=" => RegisterFlag::IsAssignment,
    "@r" => RegisterFlag::IsReadOnly,
}

pub InstructionDeclaration: MachineStatement<T> = {
    <@L> "instr" <Identifier> <Instruction> => MachineStatement::InstructionDeclaration(<>)
}

pub Instruction: Instruction<T> = {
    <params:Params> <body:InstructionBody> => Instruction { params, body }
}

pub LinkDeclaration: MachineStatement<T> = {
    <start:@L> "link" <flag:Expression> <params:Params> "=" <to:CallableRef> ";" => MachineStatement::LinkDeclaration(LinkDeclaration { start, flag, params, to })
}

pub InstructionBody: InstructionBody<T> = {
    "{}" => InstructionBody::Local(vec![]),
    "{" <InstructionBodyElements> "}" => InstructionBody::Local(<>),
    "=" <f_ref:CallableRef> => InstructionBody::CallableRef(f_ref),
}

pub CallableRef: CallableRef = {
    <instance:Identifier> "." <callable:Identifier> => CallableRef { instance, callable }
}

InstructionBodyElements: Vec<PilStatement<T>> = {
    <mut list:( <InstructionBodyElement> "," )*> <end:InstructionBodyElement>  => { list.push(end); list },
    => vec![]
}

InstructionBodyElement: PilStatement<T> = {
    PolynomialIdentity,
    PlookupIdentity,
    PermutationIdentity,
    ExpressionStatement,
}

Params: Params<T> = {
    <_input: ParamList> "->" <output: ParamList> => Params::new(_input, Some(output)),
    // we can ommit the arrow if there are no outputs
    <_input: ParamList> => Params::new(_input, None)

}

ParamList: ParamList<T> = {
    => ParamList::new(vec![]),
    <mut list:( <Param> "," )*> <end:Param>  => { list.push(end); ParamList::new(list) }
}

Param: Param<T> = {
    <name: Identifier> <index:("[" <FieldElement> "]")?> <ty:(":" <Identifier>)?> => Param{<>}
}

FunctionDeclaration: MachineStatement<T> = {
    <@L> "function" <Identifier> <Params> "{" <(<FunctionStatement>)*> "}" => MachineStatement::FunctionDeclaration(<>)
}

OperationDeclaration: MachineStatement<T> = {
    <@L> "operation" <Identifier> <OperationId> <Params> ";" => MachineStatement::OperationDeclaration(<>)
}

OperationId: OperationId<T> = {
    "<" <id:FieldElement> ">" => OperationId { id: Some(id) },
    => OperationId { id: None }
}

pub FunctionStatement: FunctionStatement<T> = {
    AssignmentStatement,
    LabelStatement,
    DebugDirectiveStatement,
    ReturnStatement,
    InstructionStatement,
}

AssignmentStatement: FunctionStatement<T> = {
    <@L> <IdentifierList> <AssignOperator> <BoxedExpression> ";" => FunctionStatement::Assignment(<>)
}

IdentifierList: Vec<String> = {
    <mut list:( <Identifier> "," )*> <end:Identifier>  => { list.push(end); list },
    => vec![]
}

AssignOperator: Option<Vec<AssignmentRegister>> = {
    "<==" => None,
    "<=" <AssignmentRegisterList> "=" => Some(<>)
}

AssignmentRegisterList: Vec<AssignmentRegister> = {
    <mut list:( <AssignmentRegister> "," )*> <end:AssignmentRegister>  => { list.push(end); list },
    => vec![]
}

AssignmentRegister: AssignmentRegister = {
    <Identifier> => AssignmentRegister::Register(<>),
    "_" => AssignmentRegister::Wildcard,
}

ReturnStatement: FunctionStatement<T> = {
    <@L> "return" <ExpressionList> ";" => FunctionStatement::Return(<>)
}

InstructionStatement: FunctionStatement<T> = {
    <@L> <Identifier> <ExpressionList> ";" => FunctionStatement::Instruction(<>)
}

DebugDirectiveStatement: FunctionStatement<T> = {
    <l:@L> "debug" "file" <n:Integer> <d:StringLiteral> <f:StringLiteral> ";"
        => FunctionStatement::DebugDirective(l, DebugDirective::File(n.try_into().unwrap(), d, f)),
    <l:@L> "debug" "loc" <f:Integer> <line:Integer> <col:Integer> ";"
        => FunctionStatement::DebugDirective(l, DebugDirective::Loc(f.try_into().unwrap(), line.try_into().unwrap(), col.try_into().unwrap())),
    <l:@L> "debug" "insn" <insn:StringLiteral> ";"
        => FunctionStatement::DebugDirective(l, DebugDirective::OriginalInstruction(insn)),
}

LabelStatement: FunctionStatement<T> = {
    <@L> <Identifier> "::" => FunctionStatement::Label(<>)
}

// ---------------------------- Expressions -----------------------------

ExpressionList: Vec<Expression<T>> = {
    => vec![],
    <mut list:( <Expression> "," )*> <end:Expression>  => { list.push(end); list }
}

Expression: Expression<T> = {
    BoxedExpression => *<>,
}

BoxedExpression: Box<Expression<T>> = {
    LambdaExpression,
}

LambdaExpression: Box<Expression<T>> = {
    "||" <body:BoxedExpression> => Box::new(Expression::LambdaExpression(LambdaExpression{params: vec![], body})),
    "|" <params:ParameterList> "|" <body:BoxedExpression> => Box::new(Expression::LambdaExpression(LambdaExpression{params, body})),
    LogicalOr
}

LogicalOr: Box<Expression<T>> = {
    <l:LogicalOr> "||" <r:LogicalAnd> => Box::new(Expression::BinaryOperation(l, BinaryOperator::LogicalOr, r)),
    LogicalAnd,
}

LogicalAnd: Box<Expression<T>> = {
    <l:LogicalAnd> "&&" <r:Comparison> => Box::new(Expression::BinaryOperation(l, BinaryOperator::LogicalAnd, r)),
    Comparison,
}

Comparison: Box<Expression<T>> = {
    <BinaryOr> <ComparisonOp> <BinaryOr> => Box::new(Expression::BinaryOperation(<>)),
    BinaryOr
}

ComparisonOp: BinaryOperator = {
    "<" => BinaryOperator::Less,
    "<=" => BinaryOperator::LessEqual,
    "==" => BinaryOperator::Equal,
    "!=" => BinaryOperator::NotEqual,
    ">=" => BinaryOperator::GreaterEqual,
    ">" => BinaryOperator::Greater,
}

BinaryOr: Box<Expression<T>> = {
    BinaryOr BinaryOrOp BinaryXor => Box::new(Expression::BinaryOperation(<>)),
    BinaryXor,
}

BinaryOrOp: BinaryOperator = {
    "|" => BinaryOperator::BinaryOr,
}

BinaryXor: Box<Expression<T>> = {
    BinaryXor BinaryXorOp BinaryAnd => Box::new(Expression::BinaryOperation(<>)),
    BinaryAnd,
}

BinaryXorOp: BinaryOperator = {
    "^" => BinaryOperator::BinaryXor,
}

BinaryAnd: Box<Expression<T>> = {
    BinaryAnd BinaryAndOp BitShift => Box::new(Expression::BinaryOperation(<>)),
    BitShift,
}

BinaryAndOp: BinaryOperator = {
    "&" => BinaryOperator::BinaryAnd,
}

BitShift: Box<Expression<T>> = {
    BitShift BitShiftOp Sum => Box::new(Expression::BinaryOperation(<>)),
    Sum,
}

BitShiftOp: BinaryOperator = {
    "<<" => BinaryOperator::ShiftLeft,
    ">>" => BinaryOperator::ShiftRight,
}

Sum: Box<Expression<T>> = {
    Sum SumOp Product => Box::new(Expression::BinaryOperation(<>)),
    Product,
}

SumOp: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
}

Product: Box<Expression<T>> = {
    Product ProductOp Power => Box::new(Expression::BinaryOperation(<>)),
    Power,
}

ProductOp: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
    "%" => BinaryOperator::Mod,
}

Power: Box<Expression<T>> = {
    <Power> <PowOp> <Term> => Box::new(Expression::BinaryOperation(<>)),
    Unary,
}

PowOp: BinaryOperator = {
    "**" => BinaryOperator::Pow,
}

Unary: Box<Expression<T>> = {
    PrefixUnaryOp PostfixUnary => Box::new(Expression::UnaryOperation(<>)),
    PostfixUnary,
}

PrefixUnaryOp: UnaryOperator = {
    "+" => UnaryOperator::Plus,
    "-" => UnaryOperator::Minus,
    "!" => UnaryOperator::LogicalNot,
}

PostfixUnary: Box<Expression<T>> = {
    <t:Term> <o:PostfixUnaryOp> => Box::new(Expression::UnaryOperation(o, t)),
    Term,
}

PostfixUnaryOp: UnaryOperator = {
    "'" => UnaryOperator::Next,
}

Term: Box<Expression<T>> = {
    IndexAccess => Box::new(Expression::IndexAccess(<>)),
    FunctionCall => Box::new(Expression::FunctionCall(<>)),
    ConstantIdentifier => Box::new(Expression::Reference(NamespacedPolynomialReference{namespace: None, name: <>})),
    NamespacedPolynomialReference => Box::new(Expression::Reference(<>)),
    PublicReference => Box::new(Expression::PublicReference(<>)),
    FieldElement => Box::new(Expression::Number(<>)),
    StringLiteral => Box::new(Expression::String(<>)),
    MatchExpression,
    IfExpression,
    "[" <items:ExpressionList> "]" => Box::new(Expression::ArrayLiteral(ArrayLiteral{items})),
    "(" <head:Expression> "," <tail:ExpressionList> ")" => { let mut list = vec![head]; list.extend(tail); Box::new(Expression::Tuple(list)) },
    "(" <BoxedExpression> ")",
    "${" <BoxedExpression> "}" => Box::new(Expression::FreeInput(<>))
}

IndexAccess: IndexAccess<T> = {
    <array:Term> "[" <index:BoxedExpression> "]" => IndexAccess{<>},
}

FunctionCall: FunctionCall<T> = {
    <function:Term> "(" <arguments:ExpressionList> ")" => FunctionCall {<>},
}

NamespacedPolynomialReference: NamespacedPolynomialReference = {
    <namespace:( <Identifier> "." )?> <name:Identifier> => NamespacedPolynomialReference{<>},
}

PublicReference: String = {
    ":" <Identifier>
}

MatchExpression: Box<Expression<T>> = {
    "match" <BoxedExpression> "{" <MatchArms> "}" => Box::new(Expression::MatchExpression(<>))
}

MatchArms: Vec<MatchArm<T>> = {
    => vec![],
    <mut list:( <MatchArm> "," )*> <end:MatchArm> ","?  => { list.push(end); list }
}

MatchArm: MatchArm<T> = {
    <pattern: MatchPattern> "=>" <value: Expression> => MatchArm{pattern, value},
}

MatchPattern: MatchPattern<T> = {
    "_" => MatchPattern::CatchAll,
    Expression => MatchPattern::Pattern(<>),
}

IfExpression: Box<Expression<T>> = {
    "if" <condition:BoxedExpression>
        "{" <body:BoxedExpression> "}"
        "else"
        "{" <else_body:BoxedExpression> "}" => Box::new(Expression::IfExpression(IfExpression{<>}))
}

// ---------------------------- Terminals -----------------------------


StringLiteral: String = {
    r#""[^"]*""# => <>[1..<>.len() - 1].to_string()
}

Identifier: String = {
    r"[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.to_string()
}

ConstantIdentifier: String = {
    // TODO it seems the lexer splits the token after %
    "%N" => <>.to_string(),
    r"%[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.to_string(),
}

FieldElement: T = {
    r"[0-9][0-9_]*" => T::from_str(&<>.replace('_', "")),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => T::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap(),
}

Integer: AbstractNumberType = {
    r"[0-9][0-9_]*" => AbstractNumberType::from_str(&<>.replace('_', "")).unwrap(),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => AbstractNumberType::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap(),
}
