use std::str::FromStr;
use std::collections::BTreeSet;
use powdr_ast::parsed::{*, asm::*, types::*};
use powdr_number::BigUint;
use crate::{ParserContext, unescape_string};

grammar(ctx: &ParserContext);

match {
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    _,
}

pub PILFile: PILFile = {
    <PilStatement*> => PILFile(<>)
};

pub ASMModule: ASMModule = {
    (<ModuleStatement>)* => ASMModule { statements: <> }
};

ModuleStatement: ModuleStatement = {
    <MachineDefinition> => ModuleStatement::SymbolDefinition(<>),
    <LetStatementAtModuleLevel> => ModuleStatement::SymbolDefinition(<>),
    <EnumDeclaration> => ModuleStatement::SymbolDefinition(SymbolDefinition {
            name: <>.name.clone(),
            value: SymbolValue::TypeDeclaration(<>),
        }),
    <Import> => ModuleStatement::SymbolDefinition(<>),
    <ModuleDefinition> => ModuleStatement::SymbolDefinition(<>),
}

ModuleDefinition: SymbolDefinition = {
    "mod" <name:Identifier> ";" => SymbolDefinition { name: name.clone(), value: Module::External(name).into() }, 
    "mod" <name:Identifier> "{" <module:ASMModule> "}" => SymbolDefinition { name, value: Module::Local(module).into() }
}

Import: SymbolDefinition = {
    "use" <path:SymbolPath> <name:( "as" <Identifier> )?> ";" =>
        SymbolDefinition {
            name: name.unwrap_or(path.name().clone().try_into().unwrap()),
            value: Import {path}.into()
        }
}

pub SymbolPath: SymbolPath = {
    <abs:"::"?> <parts:( <Part> "::" )*> <end:Part>  => {
        SymbolPath::from_parts([
            abs.map(|_| vec![Part::Named(String::new())]).unwrap_or_default(),
            parts,
            vec![end],
        ].concat())
    },
}

Part: Part = {
    "super" => Part::Super,
    <name:Identifier> => Part::Named(name),
}

/// Same as SymbolPath except that we do not allow 'int' and 'fe' to be parsed as identifiers.
TypeSymbolPath: SymbolPath = {
    <abs:"::"?> <parts:( <TypeSymbolPathPart> "::" )*> <end:TypeSymbolPathPart>  => {
        SymbolPath::from_parts([
            abs.map(|_| vec![Part::Named(String::new())]).unwrap_or_default(),
            parts,
            vec![end],
        ].concat())
    },
}

TypeSymbolPathPart: Part = {
    "super" => Part::Super,
    <name:NonSpecialIdentifier> => Part::Named(name),
}

LetStatementAtModuleLevel: SymbolDefinition = {
    "let" <name:GenericTypedName>  "=" <value:Expression> ";" =>
        SymbolDefinition {
            name: name.0,
            value: SymbolValue::Expression(TypedExpression{ e: value, type_scheme: name.1 })
        }
}

// ---------------------------- PIL part -----------------------------

pub PilStatement = {
    Include,
    Namespace,
    LetStatement,
    ConstantDefinition,
    PolynomialDefinition,
    PublicDeclaration,
    PolynomialConstantDeclaration,
    PolynomialConstantDefinition,
    PolynomialCommitDeclaration,
    <start:@L> <decl:EnumDeclaration> => PilStatement::EnumDeclaration(ctx.source_ref(start), decl),
    PlookupIdentityStatement,
    PermutationIdentityStatement,
    ConnectIdentityStatement,
    ExpressionStatement,
};

Include: PilStatement = {
    <start:@L> "include" <file:StringLiteral> ";" => PilStatement::Include(ctx.source_ref(start), file)
};

Namespace: PilStatement = {
    <start:@L> "namespace" <name:SymbolPath> "(" <pol_degree:Expression> ")" ";" => PilStatement::Namespace(ctx.source_ref(start), name, pol_degree)
}

LetStatement: PilStatement = {
    <start:@L> "let" <name:GenericTypedName> <expr:( "=" <Expression> )?> ";" =>
        PilStatement::LetStatement(ctx.source_ref(start), name.0, name.1, expr)
}

ConstantDefinition: PilStatement = {
    <start:@L> "constant" <id:ConstantIdentifier> "=" <expr:Expression> ";" => PilStatement::ConstantDefinition(ctx.source_ref(start), id, expr)
}

PolynomialDefinition: PilStatement = {
    <start:@L> PolCol <id:Identifier> "=" <expr:Expression> ";" => PilStatement::PolynomialDefinition(ctx.source_ref(start), id, expr)
}

PublicDeclaration: PilStatement = {
    <start:@L> "public" <id:Identifier> "="
        <poly:NamespacedPolynomialReference>
        <expr1:("[" <Expression> "]")?>
        "(" <expr2:Expression> ")" ";" => PilStatement::PublicDeclaration(ctx.source_ref(start), id, poly, expr1, expr2)
}

PolynomialConstantDeclaration: PilStatement = {
    <start:@L> PolCol ConstantFixed <list:PolynomialNameList> ";" => PilStatement::PolynomialConstantDeclaration(ctx.source_ref(start), list)
}

PolynomialConstantDefinition: PilStatement = {
    <start:@L> PolCol ConstantFixed <id:Identifier> <def:FunctionDefinition> ";"
        => PilStatement::PolynomialConstantDefinition(ctx.source_ref(start), id, def)
}

FunctionDefinition: FunctionDefinition = {
    "(" <params:ParameterList> ")" <body:BracedExpression> =>
        FunctionDefinition::Expression(Expression::LambdaExpression(LambdaExpression{kind: FunctionKind::Pure, params, body})),
    "=" <ArrayLiteralExpression> => FunctionDefinition::Array(<>),
}

ParameterList: Vec<String> = {
    <mut list:( <Identifier> "," )*> <end:Identifier>  => { list.push(end); list },
    => vec![]
}

ArrayLiteralExpression: ArrayExpression = {
    <ArrayLiteralExpression> "+" <ArrayLiteralTerm> => ArrayExpression::concat(<>),
    ArrayLiteralTerm,
}

ArrayLiteralTerm: ArrayExpression = {
    "[" <ExpressionList> "]" => ArrayExpression::value(<>),
    "[" <ExpressionList> "]" "*" => ArrayExpression::repeated_value(<>),
}

PolynomialCommitDeclaration: PilStatement = {
    <start:@L> PolCol CommitWitness <stage:Stage?> <list:PolynomialNameList> ";" => PilStatement::PolynomialCommitDeclaration(ctx.source_ref(start), stage, list, None),
    <start:@L> PolCol CommitWitness <stage:Stage?> <name:PolynomialName> "(" <params:ParameterList> ")" "query" <body:BoxedExpression> ";"
     => PilStatement::PolynomialCommitDeclaration(
        ctx.source_ref(start),
        stage,
        vec![name],
        Some(FunctionDefinition::Query(Expression::LambdaExpression(LambdaExpression{kind: FunctionKind::Query, params, body})))
    )
}

Stage: u32 = {
    "stage" "(" <Number> ")" => u32::try_from(<>).unwrap()
}

PolynomialNameList: Vec<PolynomialName> = {
    <mut list:( <PolynomialName> "," )*> <end:PolynomialName>  => { list.push(end); list }
}

PolynomialName: PolynomialName = {
    <name:Identifier> <array_size:( "[" <Expression> "]" )?> => PolynomialName{<>}
}

PlookupIdentityStatement: PilStatement = {
    <PlookupIdentity> ";"
}

PlookupIdentity: PilStatement = {
    <start:@L> <se1:SelectedExpressions> "in" <se2:SelectedExpressions> => PilStatement::PlookupIdentity(ctx.source_ref(start), se1, se2)
}

SelectedExpressions: SelectedExpressions<Expression> = {
    <selector:Expression?> "{" <expressions:ExpressionList> "}" => SelectedExpressions{<>},
    Expression => SelectedExpressions{selector: None, expressions: vec![<>]},
}

PermutationIdentityStatement: PilStatement = {
    <PermutationIdentity> ";"
}

PermutationIdentity: PilStatement = {
    <start:@L> <se1:SelectedExpressions> "is" <se2:SelectedExpressions> => PilStatement::PermutationIdentity(ctx.source_ref(start), se1, se2)
}

ConnectIdentityStatement: PilStatement = {
    <ConnectIdentity> ";"
}

ConnectIdentity: PilStatement = {
    <start:@L> "{" <list1:ExpressionList> "}" "connect" "{" <list2:ExpressionList> "}" => PilStatement::ConnectIdentity(ctx.source_ref(start), list1, list2)
}

ExpressionStatement: PilStatement = {
    <ExpressionStatementWithoutSemicolon> ";"
}

ExpressionStatementWithoutSemicolon: PilStatement = {
    <start:@L> <expr:Expression> => PilStatement::Expression(ctx.source_ref(start), expr)
}

PolCol = {
    "pol", "col"
} 

CommitWitness = {
    "commit", "witness"
} 

ConstantFixed = {
    "constant", "fixed"
}

GenericTypedName: (String, Option<TypeScheme<Expression>>) = {
    <name:Identifier> => (name, None),
    <vars:("<" <TypeVarBounds> ">")?> <name:Identifier> <ty:(":" <Type>)> =>
        (name, Some(TypeScheme{ vars: vars.unwrap_or_default(), ty }))
}


// ---------------------------- ASM part -----------------------------

MachineDefinition: SymbolDefinition = {
    "machine" <name:Identifier> <arguments:MachineArguments> "{" <statements:(MachineStatement)*> "}" => SymbolDefinition { name, value: Machine { arguments, statements}.into() }
}

MachineArguments: MachineArguments = {
    "(" "_" "," "_" ")" => MachineArguments::default(),
    "(" <latch:Identifier> "," "_" ")" => MachineArguments { latch: Some(latch), operation_id: None },
    "(" "_" "," <operation_id:Identifier> ")" => MachineArguments { latch: None, operation_id: Some(operation_id) },
    "(" <latch:Identifier> "," <operation_id:Identifier> ")" => MachineArguments { latch: Some(latch), operation_id: Some(operation_id) },
    => MachineArguments::default(),
}

MachineStatement: MachineStatement = {
    Degree,
    CallSelectors,
    Submachine,
    RegisterDeclaration,
    InstructionDeclaration,
    LinkDeclaration,
    PilStatementInMachine,
    FunctionDeclaration,
    OperationDeclaration,
}

PilStatementInMachine: MachineStatement = {
    <start:@L> <stmt:PilStatement> => MachineStatement::Pil(ctx.source_ref(start), stmt)
}

Degree: MachineStatement = {
    <start:@L> "degree" <deg:UnsignedInteger> ";" => MachineStatement::Degree(ctx.source_ref(start), deg)
}

CallSelectors: MachineStatement = {
    <start:@L> "call_selectors" <id:Identifier> ";" => MachineStatement::CallSelectors(ctx.source_ref(start), id)
}

Submachine: MachineStatement = {
    <start:@L> <path:SymbolPath> <id:Identifier> ";" => MachineStatement::Submachine(ctx.source_ref(start), path, id)
}

pub RegisterDeclaration: MachineStatement = {
    // TODO default update
    <start:@L> "reg" <id:Identifier> <flag:( "[" <RegisterFlag> "]" )?> ";" => MachineStatement::RegisterDeclaration(ctx.source_ref(start), id, flag)

}

RegisterFlag: RegisterFlag = {
    "@pc" => RegisterFlag::IsPC,
    "<=" => RegisterFlag::IsAssignment,
    "@r" => RegisterFlag::IsReadOnly,
}

pub InstructionDeclaration: MachineStatement = {
    <start:@L> "instr" <id:Identifier> <instr:Instruction> => MachineStatement::InstructionDeclaration(ctx.source_ref(start), id, instr)
}

pub Instruction: Instruction = {
    <params:Params> <body:InstructionBody> => Instruction { params, body }
}

pub LinkDeclaration: MachineStatement = {
    <start:@L> "link" <flag:Expression> "=>" <to:CallableRef> ";" => MachineStatement::LinkDeclaration(ctx.source_ref(start), LinkDeclaration { flag, to, is_permutation: false, }),
    <start:@L> "link" <flag:Expression> "~>" <to:CallableRef> ";" => MachineStatement::LinkDeclaration(ctx.source_ref(start), LinkDeclaration { flag, to, is_permutation: true, }),
}

pub InstructionBody: InstructionBody = {
    "{}" => InstructionBody::Local(vec![]),
    "{" <InstructionBodyElements> "}" => InstructionBody::Local(<>),
    "=" <f_ref:CallableRef> ";" => InstructionBody::CallablePlookup(f_ref),
    "~" <f_ref:CallableRef> ";" => InstructionBody::CallablePermutation(f_ref),
}

pub CallableRef: CallableRef = {
    <instance:Identifier> "." <callable:Identifier> <params:CallableParams> => CallableRef { instance, callable, params },
}

CallableParams: CallableParams = {
    <inputs: ExpressionList> "->" <output: ExpressionList> => CallableParams::new(inputs, output),
    // we can ommit the arrow if there are no outputs
    <inputs: ExpressionList> => CallableParams::new(inputs, vec![])
}

InstructionBodyElements: Vec<PilStatement> = {
    <mut list:( <InstructionBodyElement> "," )*> <end:InstructionBodyElement>  => { list.push(end); list },
    => vec![]
}

InstructionBodyElement: PilStatement = {
    PlookupIdentity,
    PermutationIdentity,
    ExpressionStatementWithoutSemicolon,
}

Params: Params<Param> = {
    <_input: ParamList> "->" <output: ParamList> => Params::new(_input, output),
    // we can ommit the arrow if there are no outputs
    <_input: ParamList> => Params::new(_input, vec![])
}

ParamList: Vec<Param> = {
    => vec![],
    <mut list:( <Param> "," )*> <end:Param>  => { list.push(end); list }
}

Param: Param = {
    <name: Identifier> <index:("[" <Number> "]")?> <ty:(":" <Identifier>)?> => Param{<>}
}

FunctionDeclaration: MachineStatement = {
    <start:@L> "function" <id:Identifier> <params:Params> "{" <stmt:(<FunctionStatement>)*> "}" => MachineStatement::FunctionDeclaration(ctx.source_ref(start), id, params, stmt)
}

OperationDeclaration: MachineStatement = {
    <start:@L> "operation" <id:Identifier> <op:OperationId> <params:Params> ";" => MachineStatement::OperationDeclaration(ctx.source_ref(start), id, op, params)
}

OperationId: OperationId = {
    "<" <id:Number> ">" => OperationId { id: Some(id.into()) },
    => OperationId { id: None }
}

pub FunctionStatement: FunctionStatement = {
    AssignmentStatement,
    LabelStatement,
    DebugDirectiveStatement,
    ReturnStatement,
    InstructionStatement,
}

AssignmentStatement: FunctionStatement = {
    <start:@L> <ids:IdentifierList> <op:AssignOperator> <expr:BoxedExpression> ";" => FunctionStatement::Assignment(ctx.source_ref(start), ids, op, expr)
}

IdentifierList: Vec<String> = {
    <mut list:( <Identifier> "," )*> <end:Identifier>  => { list.push(end); list },
    => vec![]
}

AssignOperator: Option<Vec<AssignmentRegister>> = {
    "<==" => None,
    "<=" <AssignmentRegisterList> "=" => Some(<>)
}

AssignmentRegisterList: Vec<AssignmentRegister> = {
    <mut list:( <AssignmentRegister> "," )*> <end:AssignmentRegister>  => { list.push(end); list },
    => vec![]
}

AssignmentRegister: AssignmentRegister = {
    <Identifier> => AssignmentRegister::Register(<>),
    "_" => AssignmentRegister::Wildcard,
}

ReturnStatement: FunctionStatement = {
    <start:@L> "return" <list:ExpressionList> ";" => FunctionStatement::Return(ctx.source_ref(start), list)
}

InstructionStatement: FunctionStatement = {
    <start:@L> <id:Identifier> <list:ExpressionList> ";" => FunctionStatement::Instruction(ctx.source_ref(start), id, list)
}

DebugDirectiveStatement: FunctionStatement = {
    <start:@L> ".debug" "file" <n:UnsignedInteger> <d:StringLiteral> <f:StringLiteral> ";"
        => FunctionStatement::DebugDirective(ctx.source_ref(start), DebugDirective::File(n.try_into().unwrap(), d, f)),
    <start:@L> ".debug" "loc" <f:UnsignedInteger> <line:UnsignedInteger> <col:UnsignedInteger> ";"
        => FunctionStatement::DebugDirective(ctx.source_ref(start), DebugDirective::Loc(f.try_into().unwrap(), line.try_into().unwrap(), col.try_into().unwrap())),
    <start:@L> ".debug" "insn" <insn:StringLiteral> ";"
        => FunctionStatement::DebugDirective(ctx.source_ref(start), DebugDirective::OriginalInstruction(insn)),
}

LabelStatement: FunctionStatement = {
    <start:@L> <id:Identifier> ":" => FunctionStatement::Label(ctx.source_ref(start), id)
}

// ---------------------------- Expressions -----------------------------

ExpressionList: Vec<Expression> = {
    => vec![],
    <mut list:( <Expression> "," )*> <end:Expression>  => { list.push(end); list }
}

Expression: Expression = {
    BoxedExpression => *<>,
}

BoxedExpression: Box<Expression> = {
    LambdaExpression,
}

LambdaExpression: Box<Expression> = {
    <kind:FunctionKind> "||" <body:BoxedExpression> => Box::new(Expression::LambdaExpression(LambdaExpression{kind, params: vec![], body})),
    <kind:FunctionKind> "|" <params:ParameterList> "|" <body:BoxedExpression> => Box::new(Expression::LambdaExpression(LambdaExpression{kind, params, body})),
    LogicalOr
}

FunctionKind: FunctionKind = {
    => FunctionKind::Pure,
    "query" => FunctionKind::Query,
    "constr" => FunctionKind::Constr,
}

LogicalOr: Box<Expression> = {
    <l:LogicalOr> "||" <r:LogicalAnd> => Box::new(Expression::BinaryOperation(l, BinaryOperator::LogicalOr, r)),
    LogicalAnd,
}

LogicalAnd: Box<Expression> = {
    <l:LogicalAnd> "&&" <r:Comparison> => Box::new(Expression::BinaryOperation(l, BinaryOperator::LogicalAnd, r)),
    Comparison,
}

Comparison: Box<Expression> = {
    <BinaryOr> <ComparisonOp> <BinaryOr> => Box::new(Expression::BinaryOperation(<>)),
    BinaryOr
}

ComparisonOp: BinaryOperator = {
    "<" => BinaryOperator::Less,
    "<=" => BinaryOperator::LessEqual,
    "==" => BinaryOperator::Equal,
    "=" => BinaryOperator::Identity,
    "!=" => BinaryOperator::NotEqual,
    ">=" => BinaryOperator::GreaterEqual,
    ">" => BinaryOperator::Greater,
}

BinaryOr: Box<Expression> = {
    BinaryOr BinaryOrOp BinaryXor => Box::new(Expression::BinaryOperation(<>)),
    BinaryXor,
}

BinaryOrOp: BinaryOperator = {
    "|" => BinaryOperator::BinaryOr,
}

BinaryXor: Box<Expression> = {
    BinaryXor BinaryXorOp BinaryAnd => Box::new(Expression::BinaryOperation(<>)),
    BinaryAnd,
}

BinaryXorOp: BinaryOperator = {
    "^" => BinaryOperator::BinaryXor,
}

BinaryAnd: Box<Expression> = {
    BinaryAnd BinaryAndOp BitShift => Box::new(Expression::BinaryOperation(<>)),
    BitShift,
}

BinaryAndOp: BinaryOperator = {
    "&" => BinaryOperator::BinaryAnd,
}

BitShift: Box<Expression> = {
    BitShift BitShiftOp Sum => Box::new(Expression::BinaryOperation(<>)),
    Sum,
}

BitShiftOp: BinaryOperator = {
    "<<" => BinaryOperator::ShiftLeft,
    ">>" => BinaryOperator::ShiftRight,
}

Sum: Box<Expression> = {
    Sum SumOp Product => Box::new(Expression::BinaryOperation(<>)),
    Product,
}

SumOp: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
}

Product: Box<Expression> = {
    Product ProductOp Power => Box::new(Expression::BinaryOperation(<>)),
    Power,
}

ProductOp: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
    "%" => BinaryOperator::Mod,
}

Power: Box<Expression> = {
    <Power> <PowOp> <Term> => Box::new(Expression::BinaryOperation(<>)),
    Unary,
}

PowOp: BinaryOperator = {
    "**" => BinaryOperator::Pow,
}

Unary: Box<Expression> = {
    PrefixUnaryOp PostfixUnary => Box::new(Expression::UnaryOperation(<>)),
    PostfixUnary,
}

PrefixUnaryOp: UnaryOperator = {
    "-" => UnaryOperator::Minus,
    "!" => UnaryOperator::LogicalNot,
}

PostfixUnary: Box<Expression> = {
    <t:Term> <o:PostfixUnaryOp> => Box::new(Expression::UnaryOperation(o, t)),
    Term,
}

PostfixUnaryOp: UnaryOperator = {
    "'" => UnaryOperator::Next,
}

Term: Box<Expression> = {
    IndexAccess => Box::new(Expression::IndexAccess(<>)),
    FunctionCall => Box::new(Expression::FunctionCall(<>)),
    ConstantIdentifier => Box::new(Expression::Reference(NamespacedPolynomialReference::from_identifier(<>))),
    NamespacedPolynomialReference => Box::new(Expression::Reference(<>)),
    PublicIdentifier => Box::new(Expression::PublicReference(<>)),
    Number => Box::new(Expression::Number(<>.into(), None)),
    StringLiteral => Box::new(Expression::String(<>)),
    MatchExpression,
    IfExpression,
    BlockExpression,
    "[" <items:ExpressionList> "]" => Box::new(Expression::ArrayLiteral(ArrayLiteral{items})),
    "(" <head:Expression> "," <tail:ExpressionList> ")" => { let mut list = vec![head]; list.extend(tail); Box::new(Expression::Tuple(list)) },
    "(" <BoxedExpression> ")",
    "${" <BoxedExpression> "}" => Box::new(Expression::FreeInput(<>))
}

IndexAccess: IndexAccess = {
    <array:Term> "[" <index:BoxedExpression> "]" => IndexAccess{<>},
}

FunctionCall: FunctionCall = {
    <function:Term> "(" <arguments:ExpressionList> ")" => FunctionCall {<>},
}

NamespacedPolynomialReference: NamespacedPolynomialReference = {
    <SymbolPath> => <>.into(),
    <namespace:Identifier> "." <name:Identifier> => SymbolPath::from_parts([namespace, name].into_iter().map(Part::Named)).into(),
}

MatchExpression: Box<Expression> = {
    "match" <BoxedExpression> "{" <MatchArms> "}" => Box::new(Expression::MatchExpression(<>))
}

MatchArms: Vec<MatchArm> = {
    => vec![],
    <mut list:( <MatchArm> "," )*> <end:MatchArm> ","?  => { list.push(end); list }
}

MatchArm: MatchArm = {
    <pattern: MatchPattern> "=>" <value: Expression> => MatchArm{pattern, value},
}

MatchPattern: MatchPattern = {
    "_" => MatchPattern::CatchAll,
    Expression => MatchPattern::Pattern(<>),
}

IfExpression: Box<Expression> = {
    "if" <condition:BoxedExpression>
        <body:BracedExpression>
        "else"
        <else_body:BracedExpression> => Box::new(Expression::IfExpression(IfExpression{<>}))
}

BlockExpression: Box<Expression> = {
    "{" <StatementInsideBlock+> <BoxedExpression> "}" => Box::new(Expression::BlockExpression(<>))
}

BracedExpression: Box<Expression> = {
    "{" <StatementInsideBlock+> <BoxedExpression> "}" => Box::new(Expression::BlockExpression(<>)),
    "{" <BoxedExpression> "}" => Box::new(Expression::BlockExpression(vec![], <>))
}

StatementInsideBlock: StatementInsideBlock = {
    "let" <name:Identifier> <value:("=" <Expression>)?> ";" => StatementInsideBlock::LetStatement(LetStatementInsideBlock{<>}),
    <Expression> ";" => StatementInsideBlock::Expression(<>),
}

// ---------------------------- Type Declarations -----------------------------

EnumDeclaration: EnumDeclaration<Expression> = {
    "enum" <name:Identifier> "{" <variants:EnumVariants> "}" => EnumDeclaration{<>}
}

EnumVariants: Vec<EnumVariant<Expression>> = {
    => vec![],
    <mut list:( <EnumVariant> "," )*> <end:EnumVariant> ","?  => { list.push(end); list }
}

EnumVariant: EnumVariant<Expression> = {
    <name:Identifier> <fields:("(" <TypeTermList> ")")?> => EnumVariant{<>}
}

// ---------------------------- Type Names -----------------------------

pub Type: Type<Expression> = {
    <params:TypeTermList> "->" <value:TypeTermBox> => Type::Function(FunctionType{<>}),
    TypeTerm
}

TypeTermList: Vec<Type<Expression>> = {
    => vec![],
    <mut list:( <TypeTerm> "," )*> <end:TypeTerm>  => { list.push(end); list }
}

TypeTermBox: Box<Type<Expression>> = {
    TypeTerm => Box::new(<>)
}

TypeTerm: Type<Expression> = {
    // The parser parses all identifiers as NamedTypes, some are translated
    // to TypeVars later.
    TypeSymbolPath => Type::NamedType(<>),
    "!" => Type::Bottom,
    "bool" => Type::Bool,
    "int" => Type::Int,
    "fe" => Type::Fe,
    "string" => Type::String,
    "col" => Type::Col,
    "expr" => Type::Expr,
    "constr" => Type::Constr,
    <base:TypeTerm> "[" <length:Expression?> "]" => Type::Array(ArrayType{base: Box::new(base), length}),
    "(" <mut items:( <TypeTerm> "," )+> <end:TypeTerm> ")" => { items.push(end); Type::Tuple(TupleType{items}) },
    "(" ")" => Type::Tuple(TupleType{items: vec![]}),
    "(" <Type> ")",
}

TypeVar: String = {
    UppercaseIdentifier => <>,
}


// bounds should be SymbolPaths in the future.
pub TypeVarBounds: TypeBounds = {
    => Default::default(),
    <list:( <TypeVarWithBounds> "," )*> <end:TypeVarWithBounds> => TypeBounds::new(list.into_iter().chain(std::iter::once(end)))
}

TypeVarWithBounds: (String, BTreeSet<String>) = {
    <TypeVar> <TypeBoundsList> => (<>)
}

TypeBoundsList: BTreeSet<String> = {
    => Default::default(),
    ":" <list:( <Identifier> "+" )*> <end:Identifier>  => list.into_iter().chain(std::iter::once(end)).collect(),
}


// ---------------------------- Terminals -----------------------------



StringLiteral: String = {
    r#""[^\\"\n\r]*(\\[tnfbrx'"\\0-9][^\\"\n\r]*)*""# => unescape_string(<>)
}

Identifier: String = {
    r"[a-z_][a-zA-Z$_0-9@]*" => <>.to_string(),
    UppercaseIdentifier,
    SpecialIdentifier => <>.to_string(),
}

NonSpecialIdentifier: String = {
    r"[a-z_][a-zA-Z$_0-9@]*" => <>.to_string(),
    UppercaseIdentifier => <>.to_string(),
}

UppercaseIdentifier: String = {
    r"[A-Z][a-zA-Z$_0-9@]*" => <>.to_string(),
}

// These identifier are special in the way that the lexer would treat them as keywords,
// but these keywords can never occur in a place where an identifier can be expected,
// so we allow them as identifiers as well.
SpecialIdentifier: &'input str = {
    "file",
    "loc",
    "insn",
    "int",
    "fe",
    "expr",
    "bool",
}

ConstantIdentifier: String = {
    r"%[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.to_string(),
}

PublicIdentifier: String = {
    r":[a-zA-Z_][a-zA-Z$_0-9@]*" => <>.strip_prefix(":").unwrap().to_string()

}

Number: BigUint = {
    r"[0-9][0-9_]*" => BigUint::from_str(&<>.replace('_', "")).unwrap().into(),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => BigUint::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap().into(),
}

UnsignedInteger: BigUint = {
    r"[0-9][0-9_]*" => BigUint::from_str(&<>.replace('_', "")).unwrap(),
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => BigUint::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap(),
}
