//! Much of this can be reused between front-ends, excluding elements like 
//! [FunctionKind](crate::FunctionKind) which needs to be parsed in an
//! front-end specific way.
//!
//! However it's currently difficult/impossible to share one larlpop grammar in
//! multiple places while adding slight tweaks. This requires a change to
//! larlpop like:
//! https://github.com/lalrpop/lalrpop/issues/42#issuecomment-288973232
//!
//! There are some messier solutions like, appending two larlpop files together
//! to create the "slightly tweaked" grammar, but this is not ideal.

use std::str::FromStr;
use powdr_asm_utils::ast::{unescape_string, BinaryOpKind as BOp, UnaryOpKind as UOp,
    new_binary_op as bin_op, new_unary_op as un_op, new_function_op as fn_op};
use super::super::{Argument, Register, Statement, FunctionKind as FOp, Expression, map_insn_i};
use powdr_parser_util::Error;

grammar;

extern {
    type Error = Error;
}

match {
    r"\s*" => { },
    r"#[^\n\r]*[\n\r]*" => { }, // Skip `# comments`
    r"x[0-9]",
    r"x1[0-9]",
    r"x2[0-9]",
    r"x3[0-1]",
    r"a[0-7]",
    r"s[0-1]",
    r"s[2-9]",
    r"s1[0-1]",
    r"t[0-2]",
    r"t[3-6]",
    _,
}

pub Statements: Vec<Statement> = {
    <ls:LabelOrStatement*> <s:Statement?> => ls.into_iter().flatten().chain(s.into_iter()).collect(),
}

LabelOrStatement: Option<Statement> = {
    Label => Some(<>),
    <Statement?> ";" => <>
}

Label: Statement = {
    <Symbol> ":" => Statement::Label(<>),
    <Number> ":" => Statement::Label(<>.to_string())
}

Statement: Statement = {
    SimpleDirective,
    Directive,
    Instruction,
}

SimpleDirective: Statement = {
    SimpleDirectiveName SimpleArgument* => Statement::Directive(<>),
}

SimpleDirectiveName: String = {
    ".file" => <>.to_string(),
    ".loc" => <>.to_string(),
}

Directive: Statement = {
    <DottedSymbol> <Arguments> => Statement::Directive(<>)
}

Instruction: Statement = {
    <InsnDirective>,
    <DotlessSymbol> <Arguments> => Statement::Instruction(<>)
}

InsnDirective: Statement = {
    ".insn" "i" <opcode6:Expression> "," <func3:Expression> "," <rd:Register> "," <rs1:Register> "," <simm12:Expression> => map_insn_i(opcode6, func3, rd, rs1, simm12),
    ".insn" "i" <opcode6:Expression> "," <func3:Expression> "," <rd:Register> "," <simm12:Expression> "(" <rs1:Register> ")" => map_insn_i(opcode6, func3, rd, rs1, simm12)
    // TODO: implement the other kinds of .insn instructions.
    // See https://www.rowleydownload.co.uk/arm/documentation/gnu/as/RISC_002dV_002dFormats.html
}

Arguments: Vec<Argument> = {
    => vec![],
    <mut list:( <Argument> "," )*> <end:Argument>  => { list.push(end); list }
}

SimpleArgument: Argument = {
    StringLiteral => Argument::StringLiteral(<>),
    Symbol => Argument::Expression(Expression::Symbol(<>)),
    Number => Argument::Expression(Expression::Number(<>)),
}

Argument: Argument = {
    Register => Argument::Register(<>),
    OffsetRegister,
    StringLiteral => Argument::StringLiteral(<>),
    Expression => Argument::Expression(<>),
}

Register: Register = {
    r"x[0-9]" => Register::new(<>[1..].parse().unwrap()),
    r"x1[0-9]" => Register::new(<>[1..].parse().unwrap()),
    r"x2[0-9]" => Register::new(<>[1..].parse().unwrap()),
    r"x3[0-1]" => Register::new(<>[1..].parse().unwrap()),
    "zero" => Register::new(0),
    "ra" => Register::new(1),
    "sp" => Register::new(2),
    "gp" => Register::new(3),
    "tp" => Register::new(4),
    r"a[0-7]" => Register::new(10 + <>[1..].parse::<u8>().unwrap()),
    "fp" => Register::new(8),
    r"s[0-1]" => Register::new(8 + <>[1..].parse::<u8>().unwrap()),
    r"s[2-9]" => Register::new(16 + <>[1..].parse::<u8>().unwrap()),
    r"s1[0-1]" => Register::new(16 + <>[1..].parse::<u8>().unwrap()),
    r"t[0-2]" => Register::new(5 + <>[1..].parse::<u8>().unwrap()),
    r"t[3-6]" => Register::new(25 + <>[1..].parse::<u8>().unwrap()),
}

OffsetRegister: Argument = {
    <Expression?> "(" <Register> ")" => Argument::RegOffset(<>),
}

Expression: Expression = {
    ExprBinaryOr,
}

ExprBinaryOr: Expression = {
    <l:ExprBinaryOr> "|" <r:ExprBinaryXor> => bin_op(BOp::Or, l, r),
    ExprBinaryXor,
}

ExprBinaryXor: Expression = {
    <l:ExprBinaryXor> "^" <r:ExprBinaryAnd> => bin_op(BOp::Xor, l, r),
    ExprBinaryAnd,
}

ExprBinaryAnd: Expression = {
    <l:ExprBinaryAnd> "&" <r:ExprBitShift> => bin_op(BOp::And, l, r),
    ExprBitShift,
}

ExprBitShift: Expression = {
    <l:ExprBitShift> "<<" <r:ExprSum> => bin_op(BOp::LeftShift, l, r),
    <l:ExprBitShift> ">>" <r:ExprSum> => bin_op(BOp::RightShift, l, r),
    ExprSum,
}

ExprSum: Expression = {
    <l:ExprSum> "+" <r:ExprProduct> => bin_op(BOp::Add, l, r),
    <l:ExprSum> "-" <r:ExprProduct> => bin_op(BOp::Sub, l, r),
    ExprProduct,
}

ExprProduct: Expression = {
    <l:ExprProduct> "*" <r:ExprUnary> => bin_op(BOp::Mul, l, r),
    <l:ExprProduct> "/" <r:ExprUnary> => bin_op(BOp::Div, l, r),
    <l:ExprProduct> "%" <r:ExprUnary> => bin_op(BOp::Mod, l, r),
    ExprUnary,
}

ExprUnary: Expression = {
    "~" <ExprUnary> => un_op(UOp::BitwiseNot, <>),
    "-" <ExprUnary> => un_op(UOp::Negation, <>),
    "+" <ExprUnary>,
    ExprTerm,
}

ExprTerm: Expression = {
    Number => Expression::Number(<>),
    "(" <Expression> ")" => <>,
    "%hi(" <Expression> ")" => fn_op(FOp::HiDataRef, <>),
    "%lo(" <Expression> ")" => fn_op(FOp::LoDataRef, <>),
    Symbol => Expression::Symbol(<>)
}

StringLiteral: Vec<u8> = {
    r#""[^\\"\n\r]*(\\[tnfbrx'"\\0-9][^\\"\n\r]*)*""# => unescape_string(<>)
}

DotlessSymbol: String = {
    r"[a-zA-Z_@][a-zA-Z$_0-9.@]*" => <>.to_string()
}

DottedSymbol: String = {
    r"\.[a-zA-Z_@.][a-zA-Z$_0-9.@]*" => <>.to_string()
}

Symbol: String = {
    DotlessSymbol,
    DottedSymbol,
}

Number: i64 = {
    r"[0-9][0-9_]*" => u64::from_str(<>).unwrap() as i64,
    r"0x[0-9A-Fa-f][0-9A-Fa-f_]*" => u64::from_str_radix(&<>[2..].replace('_', ""), 16).unwrap() as i64,
}
